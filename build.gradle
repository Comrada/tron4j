plugins {
    id 'java-library'
    id 'signing'
}

ext {
    bouncycastleVersion = '1.72'
    junitJupiterVersion = '5.9.1'
}

allprojects {
    version '1.0.2'
    group = 'com.github.comrada.tron4j'

    repositories {
        mavenCentral()
    }

}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    dependencies {
        testImplementation "org.junit.jupiter:junit-jupiter:$junitJupiterVersion"
    }

    test {
        useJUnitPlatform()
        testLogging {
            events 'passed', 'skipped', 'failed'
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        archiveClassifier.set('sources')
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier.set('javadoc')
        from javadoc.destinationDir
    }

    task wrapper(type: Wrapper) {
        gradleVersion = '7.6'
    }

    artifacts {
        archives jar
        archives sourcesJar
    }


    // signing {
    //     useGpgCmd()
    //     sign configurations.archives
    // }
}

