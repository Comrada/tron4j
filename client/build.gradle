plugins {
    id 'com.google.protobuf' version '0.8.19'
}

description 'Tronj gRPC client classes'

dependencies {
    implementation 'com.google.guava:guava:31.1-jre'

    implementation project(':utils')
    implementation project(':abi')

    implementation 'io.grpc:grpc-netty-shaded:1.48.0'
    implementation 'io.grpc:grpc-netty:1.48.0'
    implementation 'io.grpc:grpc-okhttp:1.48.0'
    implementation 'io.grpc:grpc-protobuf:1.48.0'
    implementation 'io.grpc:grpc-stub:1.48.0'
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53' // necessary for Java 9+
    //json parsing
    implementation 'com.google.protobuf:protobuf-java-util:3.21.3'

    implementation "org.bouncycastle:bcprov-jdk18on:$bouncycastleVersion"
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.21.3'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.48.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc { }
        }
    }
    generatedFilesBaseDir = "$projectDir/src"
}

java {
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Comrada/tron4j")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from components.java
            groupId 'com.github.comrada.tron4j'
            artifactId 'client'
            version '1.0.0'
        }
    }
}
